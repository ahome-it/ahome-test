/*
 * Copyright (c) 2014,2015 Ahome' Innovation Technologies. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(group: 'org.ajoberstar', name: 'grgit', version: '1.2.0')
    }
}

import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens
import java.net.InetAddress
import org.ajoberstar.grgit.Grgit

apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'maven'

sourceCompatibility = 1.7

version = '1.0.7'

repositories {
    mavenCentral()
    maven { url "${sonatype_snapshotURL}" }
}

sourceSets {
    main {
        resources {
            source groovy
        }
        groovy {
            srcDir 'src/main/groovy'
        }
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

configurations {
    compile.exclude(group: 'org.slf4j', module: 'log4j-over-slf4j')
    runtime.exclude(group: 'org.slf4j', module: 'log4j-over-slf4j')
}

dependencies {
    runtime(group: 'com.ahome-it', name: 'ahome-tooling-server-rpc', version: '1.0.55-SNAPSHOT')
    runtime(group: 'com.ahome-it', name: 'ahome-tooling-server-sql', version: '1.0.55-SNAPSHOT')
    runtime(group: 'com.ahome-it', name: 'ahome-tooling-server-mongodb', version: '1.0.55-SNAPSHOT')
    runtime(group: 'org.postgresql', name: 'postgresql', version: '9.4-1201-jdbc41')
    runtime(group: 'com.zaxxer', name: 'HikariCP-java6', version: '2.3.7')
    compile(group: 'com.ahome-it', name: 'ahome-tooling-server-rpc', version: '1.0.55-SNAPSHOT')
    compile(group: 'com.ahome-it', name: 'ahome-tooling-server-sql', version: '1.0.55-SNAPSHOT')
    compile(group: 'com.ahome-it', name: 'ahome-tooling-server-mongodb', version: '1.0.55-SNAPSHOT')
    compile(group: 'com.ahome-it', name: 'lienzo-core', version: '2.0.114-SNAPSHOT')
    compile(group: 'com.ahome-it', name: 'ahome-tooling-gwtdata', version: '1.0.34-SNAPSHOT')
    providedCompile(fileTree(dir: 'war/WEB-INF/lib', include: '*.jar'))
    providedCompile(group: 'com.google.gwt', name: 'gwt-dev', version: '2.7.0')
    providedCompile(group: 'com.google.gwt', name: 'gwt-user', version: '2.7.0')
    providedCompile(group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0')
}

task('gwtCompileWebApp', dependsOn: classes, type: JavaExec) {
    ext.buildDir = "${project.buildDir}/webapp"
    inputs.source sourceSets.main.groovy.srcDirs
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir buildDir
    outputs.upToDateSpec = new org.gradle.api.specs.AndSpec()
    doFirst {
        file(buildDir).mkdirs()
    }
    main = 'com.google.gwt.dev.Compiler'
    classpath {
        [
            sourceSets.main.groovy.srcDirs,
            sourceSets.main.output.classesDir,
            sourceSets.main.compileClasspath,
        ]
    }
    args =
            [
                'com.ait.ahome.Ahome',
                '-war', buildDir,
                '-logLevel', 'INFO',
                '-style', 'OBF',
                '-localWorkers', '4',
                '-XenableClosureCompiler',
            ]
    maxHeapSize = '2048M'
    minHeapSize = '2048M'
}

task('deleteConfigs', type: Delete) {
    FileTree tree = fileTree (dir: 'war/WEB-INF/classes/com/ait/ahome/server/config');
    tree.each { File file ->
        delete(file)
    }
}

task('filterConfigs', dependsOn: deleteConfigs, type: Copy) {
    from 'src/main/groovy/com/ait/ahome/server/config'
    into 'war/WEB-INF/classes/com/ait/ahome/server/config'
    filter(FixCrLfFilter)
    filter(ReplaceTokens, tokens: getFilterTokens())
}

def getFilterTokens() {
    [GRADLE_BUILD_ARTIFACT_ID: getProjectArtifactID(), GRADLE_BUILD_PROJECT_TITLE: getProjectTitle(), GRADLE_BUILD_USER: getBuildUser(), GRADLE_BUILD_DATE: getBuildDate(), GRADLE_JAVA_VERSION: getJavaVersion(), GRADLE_GIT_COMMIT_HASH: getBuildGITCommitHash(), GRADLE_GIT_COMMIT_USER: getBuildGITCommitUser(), GRADLE_GIT_COMMIT_MESSAGE: getBuildGITCommitMessage(), GRADLE_BUILD_SERVER_HOST: getBuildServerHost(), GRADLE_BUILD_SERVER_BUILD: getBuildServerBuild(), GRADLE_BUILD_MODULE_VERSION: getBuildModuleVersion()]
}

def getProjectArtifactID() {
    'ahome-test'
}

def getProjectTitle() {
    'Ahome-Test'
}

def getBuildModuleVersion() {
    version
}

def getBuildDate() {
    (new Date()).toString()
}

def getBuildServerHost() {
    try
    {
        InetAddress.getLocalHost().getHostName()
    }
    catch (Exception e)
    {
        "localhost"
    }
}

def getJavaVersion() {
    String java = System.getProperty('java.version')
    if (java)
    {
        return java
    }
    'unknown'
}

def getBuildUser() {
    String user = System.getProperty('user.name')
    if (user)
    {
        return user
    }
    'unknown'
}

def getBuildServerBuild() {
    String name = System.getProperty('BUILD_DISPLAY_NAME')
    if (name)
    {
        return name
    }
    'Command line ' + GradleVersion.current().toString()
}

def getBuildGITCommitHash() {
    def grgit = Grgit.open(project.file('.'))
    grgit.head().id
}

def getBuildGITCommitUser() {
    def grgit = Grgit.open(project.file('.'))
    grgit.head().committer.email
}

def getBuildGITCommitMessage() {
    def grgit = Grgit.open(project.file('.'))
    grgit.head().shortMessage
}

war.dependsOn(filterConfigs, gwtCompileWebApp)

war {
    manifest {
        attributes 'Implementation-Title': getProjectTitle(), 'Implementation-Version': getBuildModuleVersion()
    }
    from('war', gwtCompileWebApp.buildDir)
    webInf { from 'war/WEB-INF' }
    webXml = file('war/WEB-INF/web.xml')
    archiveName 'ahome-test.war'
}